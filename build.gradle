plugins {
    id "java"
    id "com.kazurayam.compareDirectories" version "0.1.6"
}

ext {
    workdir = "versionsDiff"
    versionA = "8.4.1"
    versionB = "8.6.0"
}

tasks.register('unpackA', Copy) {
    from zipTree("src/main/resources/${versionA}/com.kms.katalon.core.webui/com.kms.katalon.core.webui-sources.jar")
    into layout.buildDirectory.dir("${workdir}/source/${versionA}")
}

tasks.register('unpackB', Copy) {
    from zipTree("src/main/resources/${versionB}/com.kms.katalon.core.webui/com.kms.katalon.core.webui-sources.jar")
    into layout.buildDirectory.dir("${workdir}/source/${versionB}")
}

tasks.register("compareVersions", com.kazurayam.dircomp.CompareDirectoriesTask) {
    dirA = layout.buildDirectory.dir("${workdir}/source/${versionA}")
    dirB = layout.buildDirectory.dir("${workdir}/source/${versionB}")
    outputFile = layout.buildDirectory.file("${workdir}/${versionA}-${versionB}.json")
    diffDir = layout.buildDirectory.dir("${workdir}/diff")
}

tasks.register("makeZip", Zip) {
    from layout.buildDirectory.dir("${workdir}")
    include '**/*'
    archiveFileName = "KatalonStudio_versions_diff-${versionA}-${versionB}.zip"
    destinationDirectory = layout.buildDirectory
}
